buildscript {
    ext.reactiveStreamsVersion = "1.0.3"
    ext.rxjavaVersion = "3.0.5"
    ext.rxjava3DocVersion = "3.x"
    ext.jmhLibVersion = "1.21"
    ext.licenseVersion = "0.15.0"
    ext.jmhGradleVersion = "0.5.0"
    ext.junitVersion = "4.12"
    ext.jacocoVersion = "0.8.5"
    ext.pmdVersion = "5.4.2"
	ext.bndVersion = "5.1.2"
	ext.mavenPublishPluginVersion = "0.8.0"

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:$licenseVersion"
        classpath "me.champeau.gradle:jmh-gradle-plugin:$jmhGradleVersion"
        classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:$bndVersion"
        classpath "com.vanniktech:gradle-maven-publish-plugin:$mavenPublishPluginVersion"
    }
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: "me.champeau.gradle.jmh"
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: "com.github.hierynomus.license"

sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

group = "com.github.akarnokd" 
ext.githubProjectName = 'rxjava3-jdk9-interop'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''

}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public' }

     //maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    //maven { url 'https://oss.jfrog.org/libs-snapshot' }
    //maven { url 'http://repo.spring.io/libs-snapshot' }
}

dependencies {
    compile "io.reactivex.rxjava3:rxjava:${rxjavaVersion}"

    testCompile group: 'junit', name: 'junit', version: junitVersion
}

apply plugin: 'maven'

apply plugin: 'biz.aQute.bnd.builder'

jar {
    bnd ('Bundle-Name': 'rxjava3-jdk9-interop',
            'Bundle-Vendor': 'akarnokd',
            'Bundle-Description': 'RxJava 3.x extra sources, operators and components and ports of many 1.x companion libraries.',
            'Import-Package': '!org.junit,!junit.framework,!org.mockito.*,*',
            'Bundle-DocURL': 'https://github.com/akarnokd/RxJavaJdk9Interop',
			'Eclipse-ExtensibleAPI': 'true',
			'Automatic-Module-Name': 'com.github.akarnokd.rxjava3jdk9interop'
		)
}

apply plugin: "com.vanniktech.maven.publish"

jmh {
    jmhVersion = jmhLibVersion
    humanOutputFile = null
    if (project.hasProperty('jmh')) {
        include = ".*" + project.jmh + ".*"
    } else {
        include = ".*"
    }
}

plugins.withType(EclipsePlugin) {
    project.eclipse.classpath.plusConfigurations += [ configurations.jmh ]
}

javadoc {
    failOnError = false

    exclude "**/internal/**"
    exclude "**/test/**"
    exclude "**/perf/**"
    exclude "**/jmh/**"

    options.links(
        "https://docs.oracle.com/javase/9/docs/api/",
        "http://www.reactive-streams.org/reactive-streams-${reactiveStreamsVersion}-javadoc/",
        "http://reactivex.io/RxJava/$rxjava3DocVersion/javadoc/"
    )
}

test {
    maxHeapSize = "2g"
    testLogging  {
        events "started", "failed" // "skipped", "passed"
    //    showStandardStreams = true
    }
}

license {
    header rootProject.file('HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    ignoreFailures true
    excludes(["**/*.md", "**/*.txt"])
}

jacoco {
    toolVersion = jacocoVersion // See http://www.eclemma.org/jacoco/.
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

build.dependsOn jacocoTestReport

check.dependsOn jacocoTestReport

pmd {
    toolVersion = pmdVersion
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    ruleSets = []
    ruleSetFiles = files('pmd.xml')
}

pmdMain {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task pmdPrint(dependsOn: 'pmdMain') doLast {
    File file = rootProject.file('build/reports/pmd/main.xml')
    if (file.exists()) {

        println("Listing first 100 PMD violations")

        file.eachLine { line, count ->
            if (count <= 100) {
               println(line)
            }
        }

    } else {
        println("PMD file not found.")
    }
}

build.dependsOn pmdPrint
check.dependsOn pmdPrint
